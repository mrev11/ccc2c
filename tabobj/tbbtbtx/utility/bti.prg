
/*
 *  CCC - The Clipper to C++ Compiler
 *  Copyright (C) 2005 ComFirm BT.
 *
 *  This library is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU Lesser General Public
 *  License as published by the Free Software Foundation; either
 *  version 2 of the License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *  Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public
 *  License along with this library; if not, write to the Free Software
 *  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 */

//Indexelõ utility btbtx formátumhoz
//
// bti [-f]fname [-l]             //indexek listázása
// bti fname -diname              //index törlés (+összes suppindex)
// bti fname -ainame -sseg1 ...   //új index hozzáadása

#include "table.ch"

*****************************************************************************
function main()

local a:=argv(),n
local fname, iname, seg:={}
local x, v, op:="l"

    for n:=1 to len(a)

        if( !left(a[n],1)=="-" )
            fname:=a[n] 
            loop
        end

        x:=left(a[n],2)
        v:=substr(a[n],3)

        if( x=="-f" )
            fname:=v

        elseif( x=="-l" )
            op:="l"

        elseif( x=="-a" )
            iname:=v
            op:="a"
 
        elseif( x=="-d" )
            iname:=v
            op:="d"

        elseif( x=="-s" )
            aadd(seg,v)

        else
            usage("Invalid option: "+a[n])
        end
    next

    if( fname==NIL )
        usage("No input file")

    elseif( op=="l" )
        listindex(fname)
        
    elseif( op=="a" )
        addindex(fname,iname,seg)

    elseif( op=="d" )
        delindex(fname,iname)
 
    else
        usage("No operation")
    end
    ?
 
    return NIL
    
    
*****************************************************************************
static function usage(errtxt)
    if( errtxt!=NIL )
        ? errtxt
    end
    ? "Usage:" , "bti -ffile  [-aindex -sseg1 -sseg2 | -dindex | -l]"
    ?
    quit
    return NIL

*****************************************************************************
static function listindex(fname)
local t:=tabResource(fname)
local x:=tabIndex(t),n,c,i
    ? fname
    for n:=1 to len(x)
        c:=aclone(x[n][IND_COL])
        for i:=1 to len(c)
            if( valtype(c[i])=="N"  )
                c[i]:=tabColumn(t)[c[i]][COL_NAME]
            end
        next 
        ? x[n][1],if(x[n][4],"s","p"),c
    next
    ?
    return NIL


*****************************************************************************
static function addindex(fname,iname,seg)

local t:=tabResource(fname)
local b:=errorblock(),e,order

    //ha létezett az index, akkor töröljük,
    //majd az új adatokkal (újra) betesszük

    begin
        order:=tabGetIndex(t,iname)  
        adel(tabIndex(t),order)
        asize(tabIndex(t),len(tabIndex(t))-1)
    end
    tabAddIndex(t,{iname,"",seg,.f.})
    
    //az új index struktúrával megnyitjuk a filét,
    //ha az index inkompatibilis, akkor tabindexerror hiba keletkezik,
    //amit csendben továbbengedünk, és tabOpen újraépíti az indexet

    errorblock({|x|if(x:candefault,qout("build: "+iname),eval(b,e))}) 
    tabOpen(t,OPEN_EXCLUSIVE)
    errorblock(b)

    return NIL


*****************************************************************************
static function delindex(fname,iname)
local t:=tabResource(fname),o
    tabOpen(t,OPEN_EXCLUSIVE)
    o:=tabGetIndex(t,iname)  
    tabIndex(t)[o][4]:=.t.    //beállítva a suppindex flag
    tabDropindex(t)   //törli az összes suppindexet is
    return NIL
 

*****************************************************************************
 
    